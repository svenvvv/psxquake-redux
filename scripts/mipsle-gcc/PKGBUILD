# Contributor: Vojtech Horky <vojta . horky at-symbol seznam . cz>
# Contributor: Shengqi Chen <i at harrychen dot xyz>
# Contributor: Liao Junxuan <mikeljx at 126 dot com>
# Maintainer: Filippo Falezza <filippo dot falezza at outlook dot com>
# Modified by svenvvv for the psxquake-redux project

pkgname=mipsel-elf-gcc
_pkgname=gcc
_target="mipsel-elf"
pkgver=15.2.0
pkgrel=1
pkgdesc="The GNU Compiler Collection - C and C++ frontends (for baremetal MIPS)"
url="https://www.gnu.org/software/gcc/"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL' 'custom')
depends=("${_target}-binutils")
makedepends=("gcc-ada>=${pkgver:0:2}")
options=('!ccache' '!distcc' '!emptydirs' '!libtool' '!strip')
source=(https://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/${_pkgname}-${pkgver}.tar.xz{,.sig})
sha256sums=(
  'SKIP'
  'SKIP'
)

validpgpkeys=(
  F3691687D867B81B51CE07D9BBE43771487328A9  # bpiotrowski@archlinux.org
  86CFFCA918CF3AF47147588051E8B148A9999C34  # evangelos@foutrelis.com
  13975A70E63C361C73AE69EF6EEB81F8981C74C7  # richard.guenther@gmail.com
  D3A93CAD751C2AF4F8C7AD516C35B99309B5FA62  # Jakub Jelinek <jakub@redhat.com>
)

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # Hack - see native package for details
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  mkdir -p builddir
  cd builddir

  CXXFLAGS="-Wno-error=format-security" ../configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --libdir=/usr/${_target}/lib \
    --datadir=/usr/${_target}/share \
    --datarootdir=/usr/${_target}/share \
    --build=$CHOST \
    --host=$CHOST \
    --target=${_target} \
    --with-newlib \
    --with-gnu-as \
    --with-gnu-ld \
    --disable-nls \
    --disable-decimal-float \
    --with-float=soft \
    --disable-threads \
    --disable-libatomic \
    --disable-libgomp \
    --disable-libquadmath \
    --disable-libssp \
    --disable-libvtv \
    --disable-hosted-libstdcxx \
    --enable-languages=c,c++,fortran \
    --disable-libgcj \
    --enable-lto \
    --disable-werror \
    --without-headers \
    --disable-shared \
    --enable-initfini-array

  make all-gcc
  make all-target-libgcc
  make all-target-libstdc++-v3
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  cd builddir

  make DESTDIR="${pkgdir}" install-gcc
  make DESTDIR="${pkgdir}" install-target-libgcc
  make DESTDIR="${pkgdir}" install-target-libstdc++-v3

  find "$pkgdir" -name '*.la' -delete
  find "$pkgdir" -type f -executable -exec strip --strip-unneeded {} + 2>/dev/null || true
  rm -rf $pkgdir/usr/share/{man,info}
}
